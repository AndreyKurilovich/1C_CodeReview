
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
   	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ДатаСоздания = ТекущаяДатаСеанса();
	
	Состояние = ОбщегоНазначения.ПредопределенныйЭлемент("Перечисление.СостоянияЗадач.КВыполнению");
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры  

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаЗавершения 	= Неопределено;
	ДатаСоздания 	= Неопределено; 
	КачествоРешения = Неопределено;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МеткаУстановлена = Ложь;
	Для каждого УсловиеПоиска Из Условия Цикл
	
		МеткаУстановлена = Макс(МеткаУстановлена, УсловиеПоиска.МеткаВКоде);	
		
	КонецЦикла;     
	
	Если Не МеткаУстановлена Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не указана локация метки в коде'"), , , , Отказ);	
	КонецЕсли;
	
	Если Состояние = ПредопределенноеЗначение("Перечисление.СостоянияЗадач.Выполнено") Тогда
		ПроверяемыеРеквизиты.Добавить("КачествоРешения");	
	КонецЕсли;
	
	Если ВариантУсловияПоиска = ПредопределенноеЗначение("Перечисление.ВариантыУсловийПоиска.ПерваяСтрокаФайла") Тогда
		ПроверяемыеРеквизиты.Добавить("Условия");	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Состояние = Перечисления.СостоянияЗадач.ОжидаетПроверки И ДатаЗавершения = Дата(1, 1, 1) Тогда
		
			ДатаЗавершения = ТекущаяДатаСеанса();
					
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли